buildscript {
    ext.kotlin_version = '1.2.31'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

version gitVersion()

apply plugin: 'kotlin'

dependencies {
    compile project(':keanu-project')

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
task writeSnippets(type:Exec) {
    commandLine 'python', 'bin/snippet_writer.py'
}

task installDependencies {
    doLast {
        exec {
            workingDir("src/test/python/")
            commandLine 'pipenv', 'install'
        }
    }
}

task installPipenv {
    doLast {
        exec {
            commandLine 'pip', 'install', 'pipenv==2018.11.26'
        }
    }
}

task prepareKeanuPipenv {
    dependsOn(installPipenv)
    dependsOn(":keanu-python:build")
    dependsOn(installDependencies)
}

task runPythonSnippets {
    dependsOn(prepareKeanuPipenv)
    doLast {
        exec {
            workingDir("src/test/python/")
            commandLine 'pipenv', 'run', 'pytest', '.'
        }
    }
}

build.finalizedBy(writeSnippets)
test.dependsOn(runPythonSnippets)