buildscript {
    ext.kotlin_version = '1.2.31'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

version gitVersion()

apply plugin: 'kotlin'

dependencies {
    compile project(':keanu-project')

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
task writeSnippets(type:Exec) {
    commandLine 'python', 'bin/snippet_writer.py'
}

task installDependencies {
    doLast {
        exec {
            workingDir("src/test/python/")
            executable("pipenv")
            args(["install"])
        }
    }
}

task prepareKeanuPipenv {
    dependsOn(":keanu-python:installPipenv")
    dependsOn(":keanu-python:build")
    dependsOn(installDependencies)
}

task runPythonSnippets {
    dependsOn(prepareKeanuPipenv)
    doLast {
        exec {
            workingDir("src/test/python/")
            executable("pipenv")
            args(["run", "pytest", "."])
        }
    }
}

build.finalizedBy(writeSnippets)
test.dependsOn(runPythonSnippets)